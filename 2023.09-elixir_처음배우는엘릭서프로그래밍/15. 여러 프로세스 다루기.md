# 15. 여러 프로세스 다루기

## 1. 액터모델

* 엘릭서에서는 액터모델(Actor Model)을 사용한다.

* 액터란 다른 프로세스와 데이터를 전혀 공유하지 않는 독립적인 프로세스를 말한다.

* 엘릭서에서 프로세스는 무겁고 느린 운영체제의 프로세스가 아니다.

* 엘릭서는 얼랭이 제공하는 프로세스를 사용하는데, 얼랭 프로세스는 일반적인 프로세스와 마찬가지로 모든 CPU에서 실행되지만 부하는 매우 적다.

* spawn : 프로세스를 만든다.

* send : 프로세스에 메세지를 보낸다.

* receive : 돌아오는 메세지를 받는다.

## 2. 프로세스 다루기

### 2.1. 간단한 프로세스

* 아래의 코드를 작성하고 iex 컴파일 한다.

```elixir
defmodule SpawnBasic do
  def greet do
    IO.puts "hello"
  end
end
```

```elixir
iex(1)> c("spawn-basic.ex")
[SpawnBasic]

# 함수 실행
iex(2)> SpawnBasic.greet
hello
:ok

iex(3)> spawn(SpawnBasic, :greet, [])
hello
#PID<0.117.0>
```

### 2.2. 프로세스간 메세지 주고 받기

* pid와 send를 이용해서 메세지를 송신하고 receive를 통해서 메세지를 수신할 수 있다.

```elixir
defmodule Spawn1 do
  def greet do
    receive do
      {sender, msg} -> 
        send sender, { :ok, "hello, #{msg}" }
    end
  end
end

pid = spawn(Spawn1, :greet, [])
send pid, {self(), "world!!"}

receive do
  {:ok, message} -> 
    IO.puts message
end
```

```elixir
iex(4)> c("spawn1.ex")
hello, world!!
[Spawn1]
```

### 2.3. 여러개 메세지 주고 받기

* 예제코드를 조금 수정해서 메세지를 하나더 보내고 받아본다.

```elixir
defmodule Spawn1 do
  def greet do
    receive do
      {sender, msg} -> 
        send sender, { :ok, "hello, #{msg}" }
    end
  end
end

pid = spawn(Spawn1, :greet, [])
send pid, {self(), "world!!"}

receive do
  {:ok, message} -> 
    IO.puts message
end

send pid, {self(), "second world!!"}

receive do
  {:ok, message} -> 
    IO.puts message
end
```

* 첫번째 메세지 수신후 터미널이 멈춘다. ctrl+c 를 이용해서 나와야 한다.

* 이유는 greet 메소드가 하나의 메세지만 처리 할수 있기 때문이다.

```elixir
iex(5)> c("spawn2.ex")
warning: redefining module Spawn1 (current version defined in memory)
  spawn2.ex:1: Spawn1 (module)

hello, world!!
```

### 2.4. after를 이용해 멈춤 개선하기

* after 지시어를 사용해서 타임아웃 처리를 한다.

```elixir
defmodule Spawn1 do
  def greet do
    receive do
      {sender, msg} -> 
        send sender, { :ok, "hello, #{msg}" }
    end
  end
end

pid = spawn(Spawn1, :greet, [])
send pid, {self(), "world!!"}

receive do
  {:ok, message} -> 
    IO.puts message
end

send pid, {self(), "second world!!"}

receive do
  {:ok, message} -> 
    IO.puts message
  after 500 -> 
    IO.puts "The greeter has gone away"
end
```

* 타임아웃에 해당하는 메세지가 출력된다.

```elixir
iex(1)> c("spawn2.ex")
hello, world!!
The greeter has gone away
[Spawn1]
```

### 2.5. 여러번 처리하기

* 엘릭서에는 반복문이 없다. 재귀를 이용하자.

```elixir
defmodule Spawn1 do
  def greet do
    receive do
      {sender, msg} -> 
        send sender, { :ok, "hello, #{msg}" }
      greet()
    end
  end
end

pid = spawn(Spawn1, :greet, [])
send pid, {self(), "world!!"}

receive do
  {:ok, message} -> 
    IO.puts message
end

send pid, {self(), "second world!!"}

receive do
  {:ok, message} -> 
    IO.puts message
  after 500 -> 
    IO.puts "The greeter has gone away"
end
```

* 이제 다시 실행하면 정상적으로 두번째 메세지도 수신한다.

```elixir
iex(2)> c("spawn2.ex")
warning: redefining module Spawn1 (current version defined in memory)
  spawn2.ex:1: Spawn1 (module)

hello, world!!
hello, second world!!
[Spawn1]
```

## 3. 재귀, 꼬리 재귀 최적화

* 위와 같이 재귀로 호출하면 함수 호출마다 스택에 프레임이 추가되므로 성능상 문제가 생긴다.

* 하지만 엘릭서의 재귀는 `꼬리 재귀 최적화(tail-call optimization)`가 구현되어 성능상 문제가 없다.

* 단, 꼬리 재귀 최적화가 적용되려면, 반드시 재귀호출이 함수의 마지막에서 이뤄져야 한다.

* 예시로 아래의 경우는 꼬리재귀가 아니다. 재귀 호출이 마지막에 있기는 하지만, 값을 반환한 다음에도 곱하기 연산이 남아 있기 때문이다.

```elixir
def factorial(0), do: 1
def factorial(n), do: n * factorial(n-1)
```

* 위 케이스를 꼬리재귀로 변경하려면 곱셈을 재귀호출 안으로 옮겨야 한다.

```elixir
defmodule TailRecursive do
  def factorial(n), do: _fact(n, 1)
  def _fact(0, acc), do: acc
  def _fact(n, acc), do: _fact(n-1, acc*n)
end
```

## 4. 프로세스 부하

* 프로세스 부하를 위해 아래 코드를 실행한다.

* TO-DO : 코드가 정확히 이해가 되지는 않는다. 나중에 정리 필요

```elixir
defmodule Chain do
  def counter(next_pid) do
    receive do
      n ->
        send next_pid, n + 1
    end
  end

  def create_processes(n) do
    code_to_run = fn (_, send_to) ->
      spawn(Chain, :counter, [send_to])
    end

    last = Enum.reduce(1..n, self(), code_to_run)
    send(last, 0) # 마지막 프로세스에 0을 보내 카운트를 시작한다.

    receive do # 그리고 결과가 돌아올때까지 기다린다.
      final_answer when is_integer(final_answer) ->
        "Result is #{inspect(final_answer)}"
    end
  end

  def run(n) do
    :timer.tc(Chain, :create_processes, [n])
    |> IO.inspect
  end
end
```

* 성능 측정을 위해 이번에는 터미널에서 실행한다.

* 100만개의 프로세스를 실행하기 위해서 VM 옵션 +P 옵션을 통해 limit을 높인다.

* 실행결과는 나노초이다. 즉, 1000이 1밀리 세컨드이다.

```bash
mhlee@pop-os:/tmp$ elixir -r chain.exs -e "Chain.run(100)"
{2392, "Result is 100"}

mhlee@pop-os:/tmp$ elixir -r chain.exs -e "Chain.run(100_000)"
{321658, "Result is 100000"}

mhlee@pop-os:/tmp$ elixir -r chain.exs -e "Chain.run(100_000)"
{309021, "Result is 100000"}

mhlee@pop-os:/tmp$ elixir -r chain.exs -e "Chain.run(1_000_000)"

00:06:12.268 [error] Too many processes

#에러 발생!!

mhlee@pop-os:/tmp$ elixir --erl "+P 1000000" -r chain.exs -e "Chain.run(1_000_000)"
{2948332, "Result is 1000000"}

## 100만개가 실행되었다.
```

## 5. 프로세스 상태

### 5.1. 프로세스가 종료될때

* 프로세스가 종료되어도 아무도 모른다. VM은 알수 있지만 별도로 알리지 않는다.

```elixir
defmodule Link1 do
  import :timer, only: [ sleep: 1 ]

  def sad_function do
    sleep 500
    exit(:boom)
  end

  def run do
    spawn(Link1, :sad_function, [])
    receive do
      msg ->
        IO.puts "MESSAGE RECEIVED: #{inspect msg}"
    after 1000 ->
        IO.puts "Nothing happened as far as I am concerned"
    end
  end
end

Link1.run
```


* 위 예제를 실행하면, 프로세스 종료사실은 모르고 1초안에 메세지가 수신되지 않았기 때문에 해당 메세지만 출력한다.

```bash
mhlee@pop-os:/tmp$ elixir -r link1.exs
Nothing happened as far as I am concerned
```

### 5.2. 프로세스 링크하기

* spawn_link를 통해 상태를 공유할수 있다.

```elixir
defmodule Link2 do
  import :timer, only: [ sleep: 1 ]

  def sad_function do
    sleep 500
    exit(:boom)
  end

  def run do
    spawn_link(Link2, :sad_function, [])
    receive do
      msg ->
        IO.puts "MESSAGE RECEIVED: #{inspect msg}"
    after 1000 ->
        IO.puts "Nothing happened as far as I am concerned"
    end
  end
end

Link2.run
```

* 위 예제를 실행하면, 프로세스가 비정상적으로 종료되면 런타임이 이를 알려준다.

* 자식 프로세스가 종료되자 전체 애플리케이션이 종료되었다.

```bash
mhlee@pop-os:/tmp$ elixir -r link2.exs
** (EXIT from #PID<0.94.0>) :boom
```

### 5.3. 프로세스 종료 신호 핸들링하기

* Process.flag(:trap_exit, true) 구문을 사용해서 종료신호를 처리할수 있다.

```elxir
defmodule Link3 do
  import :timer, only: [ sleep: 1 ]

  def sad_function do
    sleep 500
    exit(:boom)
  end

  def run do
    Process.flag(:trap_exit, true)
    spawn_link(Link3, :sad_function, [])
    receive do
      msg ->
        IO.puts "MESSAGE RECEIVED: #{inspect msg}"
    after 1000 ->
        IO.puts "Nothing happened as far as I am concerned"
    end
  end
end

Link3.run
```

* 이제 프로세스가 비정상 종료되지 않는다.

```bash
mhlee@pop-os:/tmp$ elixir -r link3.exs
MESSAGE RECEIVED: {:EXIT, #PID<0.99.0>, :boom}
```

### 5.4. 프로세스 모니터링 하기

* spawn_monitor을 이용해서 모니터링 가능하다.

* spawn_link는 양방향인데 반해, spawn_monitor는 단방향이다.

* 즉, 자식 -> 부모 방향으로만 상태를 전송한다.

```elixir
defmodule Link4 do
  import :timer, only: [ sleep: 1 ]

  def sad_function do
    sleep 500
    exit(:boom)
  end

  def run do
    res = spawn_monitor(Link4, :sad_function, [])
    IO.puts inspect res
    receive do
      msg ->
        IO.puts "MESSAGE RECEIVED: #{inspect msg}"
    after 1000 ->
        IO.puts "Nothing happened as far as I am concerned"
    end
  end
end

Link4.run
```

* 실행하면 정상적으로 프로세스 상태를 수신한다.

```bash
mhlee@pop-os:/tmp$ elixir -r link4.exs
{#PID<0.99.0>, #Reference<0.1393725979.3876847632.26967>}
MESSAGE RECEIVED: {:DOWN, #Reference<0.1393725979.3876847632.26967>, :process, #PID<0.99.0>, :boom}
```