# 12. 제어구문

## 1. 살펴볼 내용

* if와 unless

* cond (if의 여러 조건 버전)

* case (switch의 패턴 매칭 버전)

* 예외

## 2. 엘릭서에서 제어구문

* 엘릭서는 명령형을 지양하고 선언형을 지향

* 엘릭서로 코드를 작성하다보면 작은 함수를 많이 만들게 됨

* 함수 + 가드 조건절과 파라미터 패턴 매칭을 조합하면 다른 언어의 제어 구문을 대부분 대체할 수 있음

## 3. if와 unless

```elxir
iex(48)> if 1 == 1, do: "true", else: "false"
"true"

iex(49)> if 1 == 2, do: "true", else: "false"
"false"

# 아래와 같이 표현 가능
iex(50)> if 1 == 1 do
...(50)>   "true"
...(50)> else
...(50)>   "false"
...(50)> end
"true"

# unless는 if와 정반대의 성격의 쌍둥이
iex(51)> unless 1 == 1, do: "true", else: "false"
"false"

iex(52)> unless 1 == 2, do: "true", else: "false"
"true"
```

## 4. cond

* FizzBuzz 게임을 구현해본다.

```elixir
# FizzBuzz.ex
defmodule FizzBuzz do
  def upto(n) when n > 0, do: _upto(1, n, [])

  defp _upto(_current, 0, result), do: Enum.reverse result

  defp _upto(current, left, result) do
    next_answer = cond do
      rem(current, 3) == 0 and rem(current, 5) == 0 -> "FizzBuzz"
      rem(current, 3) == 0 -> "Fizz"
      rem(current, 5) == 0 -> "Buzz"
      true -> current
    end

    _upto(current+1, left-1, [next_answer | result])
  end
end
```

```elixir
iex(1)> c "fizzbuzz.ex"
[FizzBuzz]

iex(2)> FizzBuzz.upto(20)
[1, 2, "Fizz", 4, "Buzz", "Fizz", 7, 8, "Fizz", "Buzz", 11, "Fizz", 13, 14,
 "FizzBuzz", 16, 17, "Fizz", 19, "Buzz"]
```

* 마지막에서 reverse 한것은 스택처럼 마지막 값이 맨앞에 있기 때문이다.

* 이를 조금 개선해보자.

```elixir
defmodule FizzBuzz do
  def upto(n) when n > 0, do: _downto(n, [])

  defp _downto(0, result), do: result

  defp _downto(current, result) do
    next_answer = cond do
      rem(current, 3) == 0 and rem(current, 5) == 0 -> "FizzBuzz"
      rem(current, 3) == 0 -> "Fizz"
      rem(current, 5) == 0 -> "Buzz"
      true -> current
    end

    _downto(current-1, [next_answer | result])
  end
end
```

* 코드는 개선되었지만, 사람들은 1부터 올라가면서 로직을 생각하기 때문에 혼동이 있을수 있다.

* 함수형으로 조금 더 개선해보자.

```elixir
defmodule FizzBuzz do
  def upto(n) when n > 0 do
    1..n |> Enum.map(&fizzbuzz/1)
  end

  defp fizzbuzz(n) do
    cond do
      rem(n, 3) == 0 and rem(n, 5) == 0 -> "FizzBuzz"
      rem(n, 3) == 0 -> "Fizz"
      rem(n, 5) == 0 -> "Buzz"
      true -> n
    end
  end
end
```

* cond를 사용해도 되지만, 패턴매칭을 사용하면 더 함수적으로 표현된다.

```elixir
defmodule FizzBuzz do
  def upto(n) when n > 0 do
    1..n |> Enum.map(&fizzbuzz/1)
  end

  defp fizzbuzz(n), do: _fizzbuzz(n, rem(n, 3), rem(n, 5))

  defp _fizzbuzz(_n, 0, 0), do: "FizzBuzz"
  defp _fizzbuzz(_n, 0, _), do: "Fizz"
  defp _fizzbuzz(_n, _, 0), do: "Buzz"
  defp _fizzbuzz(n, _, _), do: n
end
```

## 5. case

* case는 하나의 값을 여러 패턴에 대해 확인해, 매칭되는 첫번째 해당하는 코드를 실행하고 그 연산결과를 리턴한다.

```elixir
case File.open("case11.ex") do
  {:ok, file} -> IO.puts "Read data: #{IO.read(file, :line)}"
  {:error, reason} -> IO.puts "Error: #{reason}"
end
```