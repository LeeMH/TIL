# 5. 익명함수

## 1. 익명함수 생성

* 엘릭서는 함수형 언어이고, 함수가 기본타입 이다.

* 아래와 같이 `fn .. end` 형태로 선언 가능하다.

* 기명 함수와 다르게 함수명 뒤에 온점을 찍고 파라메터를 넣어주어야 한다.

```elixir
iex(1)> sum = fn (a, b) -> a + b end
#Function<43.65746770/2 in :erl_eval.expr/5>
iex(2)> sum.(1, 2)
3
```

* 파라메터부의 괄호는 생략가능하지만, 호출할때는 파라메터가 없더라도 온점과 괄호를 넣고 호출해야 한다.

```elixir
iex(5)> hello = fn -> IO.puts "hello" end
#Function<45.65746770/0 in :erl_eval.expr/5>
iex(6)> hello
#Function<45.65746770/0 in :erl_eval.expr/5>
iex(7)> hello.()
hello
:ok
iex(8)> mul = fn a, b -> a * b end
#Function<43.65746770/2 in :erl_eval.expr/5>
iex(9)> mul.(2, 4)
8
```

## 2. 함수는 하나, 본문은 여러개

* 엘릭서는 패턴매칭을 통해 OOP의 오버로딩과 비슷한 효과를 낼수 있다.

* 파라메터가 함수 선언부와 매칭되는 본문이 실행되는 구조이다.

```elixir
iex(14)> handle_open = fn
...(14)> {:ok, file} -> "read data: #{IO.read(file, :line)}"
...(14)> {_, error} -> "error: #{:file.format_error(error)}"
...(14)> end
#Function<44.65746770/1 in :erl_eval.expr/5>
iex(15)> handle_open.(File.open("/etc/hosts"))
"read data: 127.0.0.1\tlocalhost\n"
iex(16)> handle_open.(File.open("/etc/hosts-not-exists"))
"error: no such file or directory"
```

* error 처리부의 :file은 얼랭의 모듈을 가르킨다.

## 3. 함수를 반환하는 함수

```elixir
iex(19)> fun1 = fn -> fn -> "hello" end end
#Function<45.65746770/0 in :erl_eval.expr/5>
iex(20)> fun1.()
#Function<45.65746770/0 in :erl_eval.expr/5>
iex(21)> fun1.().()
"hello"
```

## 4. 함수는 자신의 원래 환경을 기억한다

```elixir
iex(1)> greeter = fn name -> (fn -> "hello #{name}" end) end
#Function<44.65746770/1 in :erl_eval.expr/5>
iex(3)> f1 = greeter.("mhlee")
#Function<45.65746770/0 in :erl_eval.expr/5>
iex(4)> f2 = greeter.("dave")
#Function<45.65746770/0 in :erl_eval.expr/5>
iex(5)> f1.()
"hello mhlee"
iex(6)> f2.()
"hello dave"
```

## 5. 모든 함수가 파라메터를 가진 경우

```elixir
iex(9)> add_n = fn n -> (fn other -> n + other end) end
#Function<44.65746770/1 in :erl_eval.expr/5>
iex(10)> add2 = add_n.(2)
#Function<44.65746770/1 in :erl_eval.expr/5>
iex(11)> add5 = add_n.(5)
#Function<44.65746770/1 in :erl_eval.expr/5>
iex(12)> add2.(3)
5
iex(13)> add5.(3)
8
```

## 6. 함수를 인자로 전달하기

* 엘릭서는 함수형 언어로 당연히 함수자체를 파라메터로 전달할수 있다.

```elixir
iex(16)> times2 = fn n -> n * 2 end
#Function<44.65746770/1 in :erl_eval.expr/5>
iex(17)> apply = fn (fun, value) -> fun.(value) end
#Function<43.65746770/2 in :erl_eval.expr/5>
iex(18)> apply.(times2, 6)
12
```

* 엘릭서는 함수를 인자로 받는 경우가 흔하며, 대표적인 예가 Enum의 map이다.

```elixir
iex(19)> list = [1, 3, 5, 7]
[1, 3, 5, 7]
iex(20)> Enum.map list, fn x -> x + 1 end
[2, 4, 6, 8]
iex(21)> Enum.map list, fn x -> x > 3 end
[false, false, true, true]
```

## 7. 함수 파라메터 고정하기

* 캐럿(^) 연산자를 이용해 패턴안의 변수를 고정시킬수 있다.

```elixir
iex(24)> defmodule Greeter do
...(24)>   def for(name, greeting) do
...(24)>     fn 
...(24)>       (^name) -> "#{greeting}, #{name}"
...(24)>       (_) -> "I don't know you"
...(24)>     end
...(24)>   end
...(24)> end
{:module, Greeter,
 <<70, 79, 82, 49, 0, 0, 6, 164, 66, 69, 65, 77, 65, 116, 85, 56, 0, 0, 0, 194,
   0, 0, 0, 19, 14, 69, 108, 105, 120, 105, 114, 46, 71, 114, 101, 101, 116,
   101, 114, 8, 95, 95, 105, 110, 102, 111, 95, ...>>, {:for, 2}}
iex(25)> 
nil
iex(26)> mr_lee = Greeter.for("mhlee", "안녕")
#Function<0.43048118/1 in Greeter.for/2>
iex(27)> IO.puts mr_lee.("mhlee")
안녕, mhlee
:ok
iex(28)> IO.puts mr_lee.("mike")
I don't know you
:ok
```

## 8. & 표기법

* 함수를 만들때 `&`를 이용한 단축 문법이 존재한다.

```elixir
iex(31)> add_one = &(&1 + 1)
#Function<44.65746770/1 in :erl_eval.expr/5>
iex(32)> add_one.(9)
10
iex(33)> mul_two = &(&1 * &2)
&:erlang.*/2
iex(34)> mul_two.(3, 4)
12
```

* 리스트와 튜플 리터럴도 함수가 될수 있다

```elixir
iex(35)> divrem = &{div(&1, &2), rem(&1, &2)}
#Function<43.65746770/2 in :erl_eval.expr/5>
iex(36)> divrem.(9, 2)
{4, 1}
```

* & 표기법은 문자열과 관련된 리터럴에서도 동작한다

```elixir
iex(39)> s = &"bacon & #{&1}"
#Function<44.65746770/1 in :erl_eval.expr/5>
iex(40)> s.("ham")
"bacon & ham"

iex(42)> match_end = &~r/.*#{&1}$/
#Function<44.65746770/1 in :erl_eval.expr/5>
iex(43)> "cat" =~ match_end.("t")
true
iex(44)> "cat" =~ match_end.("!")
false
```

## 9. 캡쳐함수의 다른 사용법

* `&(IO.puts(&1))` 이식은 `&IO.puts/1` 이렇게 줄여 쓸수 있다.

* 아래는 이러한 축약식의 예시이다.

```elixir
iex(47)> l = &length/1
&:erlang.length/1
iex(48)> l.([1, 2, 3])
3

iex(49)> m = &Kernel.min/2
&:erlang.min/2
iex(50)> m.(3, 4)
3
```