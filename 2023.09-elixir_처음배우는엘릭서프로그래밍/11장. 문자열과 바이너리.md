# 11. 문자열과 바이너리

## 1. 살펴볼 내용

* `문자열`과 `문자열 리터럴`

* `문자 리스트`(작은 따옴표 리터럴)

* 패턴 매칭과 문자열 처리

## 2. 문자열 리터럴

* 엘릭서에는 두종류의 문자열 존재

    + 작은 따옴표로 감싼것

    + 큰 따옴표로 감싼것

* 둘은 내부 구현이 매우 다르지만, 공통점도 많다.

    + UTF-8 인코딩된 문자를 저장할 수 있다.

    + 이스케이프 문자를 포함할 수 있다.

    + #{..} 문법으로 엘릭서 표현식을 삽입할 수 있다.

    + 특별한 의미를 가진 문자는 백슬래시를 사용해 이스케이프

    + 히어독스(heredocs) 표기법 지원

## 3. 시길

* 루비와 비슷하게 일부 리터럴에 대해서 대체 문법 제공

    + ~C : 이스케이프 또는 문자열 삽입을 `수행하지 않는` 문자 리스트

    + ~c : 이스케이프 또는 문자열 삽입을 `수행하는` 문자 리스트. 작은 따옴표 문자열과 같음

    + ~D : yyyy-mm-dd 형식의 날짜로 타입은 `Date`

    + ~d : yyyy-mm-dd hh:mm:ss[.ddd] 형식의 `NativeDateTime`

    + ~R : 이스케이프 또는 문자열 삽입을 `수행하지 않는` 정규식

    + ~r : 이스케이프 또는 문자열 삽입을 `수행하는` 정규식

    + ~S : 이스케이프 또는 문자열 삽입을 `수행하지 않는` 문자열

    + ~s : 이스케이프 또는 문자열 삽입을 `수행하는` 문자열. 큰 따옴표 문자열과 같음.

    + ~T : hh:mm:ss[.ddd] 형식의 시간으로 타입은 `Time`

    + ~U : ISO-8601 형식의 날짜로, 타입은 `DateTime`

    + ~W : 공백 문자로 값을 구분하는 리스트로, 이스케이프 또는 문자열 삽입을 `수행하지 않음`

    + ~w : 공백 문자로 값을 구분하는 리스트. 이스케이프 또는 문자열 삽입을 `수행`

* 시길 예시

```elixir
iex(1)> ~C[1\n2#{1+2}] 
'1\\n2\#{1+2}'

iex(2)> ~c[1\n2#{1+2}]
'1\n23'

iex(3)> ~S[1\n2#{1+2}]
"1\\n2\#{1+2}"

iex(4)> ~s[1\n2#{1+2}]
"1\n23"

iex(5)> ~W[the c#{'a'}t sat on the mat]
["the", "c\#{'a'}t", "sat", "on", "the", "mat"]

iex(6)> ~w[the c#{'a'}t sat on the mat]
["the", "cat", "sat", "on", "the", "mat"]

iex(7)> ~D<1999-12-31>
~D[1999-12-31]

iex(8)> ~T[12:34:56]
~T[12:34:56]

iex(9)> ~N{1999-12-31 23:59:59}
~N[1999-12-31 23:59:59]

# 히어독스로 시작하면 이 시길은 히어독스로 취급
iex(10)> ~w"""
...(10)> cat
...(10)> dog
...(10)> sat
...(10)> """
["cat", "dog", "sat"]
```

> 내용을 감싸는 구분자는 단어가 아닌 문자라면 무엇이든 괜찮다.

## 4. 작은 따옴표 문자열은 문자리스트다!!

* 작은 따옴표 문자열은 내부적으로 `정수 리스트`로 표현된다.

* 이때 정수값은 유니코드 코드포인트

```elixir
iex(16)> str = 'wombat'
'wombat'
iex(17)> is_list str
true
iex(18)> length str
6
iex(19)> Enum.reverse str
'tabmow'
```

* 리스트인데 문자열 처럼 출력되는 이유는, 리스트의 값이 모두 출력 가능한 문자의 코드포인트라면 iex가 리스트를 문자열로 출력하기 때문이다.

```elixir
iex(22)> [67, 65, 84]
'CAT'

iex(23)> str = 'wombat'
'wombat'

iex(24)> :io.format "~w~n", [ str ]
[119,111,109,98,97,116]
:ok

iex(25)> List.to_tuple str
{119, 111, 109, 98, 97, 116}

iex(26)> str ++ [0]
[119, 111, 109, 98, 97, 116, 0]
```

* 문자리스트 역시 리스트이므로 패턴 매칭과 List 모듈의 함수를 사용할 수 있다.

```elixir
iex(28)> 'pole' ++ 'valut'
'polevalut'

iex(29)> 'pole' -- 'valut'
'poe'

iex(30)> List.zip ['abc', '123']
[{97, 49}, {98, 50}, {99, 51}]

iex(31)> [ head | tail ] = 'cat'
'cat'

iex(32)> head
99

iex(33)> tail
'at'

iex(34)> [head | tail]
'cat'
```

## 5. 바이너리

* 바이너리 타입은 연속된 `비트`를 나타낸다.

* 바이너리의 리터럴은 `<<term, ...>>`과 같이 쓴다.

* 가장 단순한 단위 값은 `0부터 255` 사이의 숫자 하나로, 이 숫자들이 바이너리 안에 연속된 바이트로 저장된다.

```elixir
iex(35)> b = <<1, 2, 3>>
<<1, 2, 3>>

iex(36)> byte_size b
3

iex(37)> bit_size b
24
```

* 각 값은 기본적으로 1바이트를 차지하지만 데이터 크기를 비트 단위로 설정가능

* 미디어 파일이나 네트워크 패킷 같은 바이너리 포맷을 다룰때 유용

```elixir
# 01 001 => 9
iex(40)> b = <<1::size(2), 1::size(3)>>
<<9::size(5)>>

iex(41)> byte_size b
1

iex(42)> bit_size b
5
```

* 바이너리에는 정수외에 실수나 다른 바이너리도 저장할 수 있다.

```elixir
iex(45)> int = <<1>> 
<<1>>

iex(46)> float = <<2.5::float>>
<<64, 4, 0, 0, 0, 0, 0, 0>>

iex(47)> mix = <<int::binary, float::binary>>
<<1, 64, 4, 0, 0, 0, 0, 0, 0>>

iex(48)> byte_size mix
9

iex(49)> byte_size int
1

iex(50)> byte_size float
8
```

## 6. 큰따옴표 문자열은 바이너리다.

* 큰따옴표 문자열은 연속된 바이트에 UTF-8 인코딩되어 저장된다.

* 따라서, 문자열의 길이와 byte_size는 다를수 있다.

## 7. 문자열과 엘릭서 라이브러리

* 엘릭서 라이브러리 문서에서 `문자열(string)`, `바이너리(binary)`라는 용어는 항상 큰따옴표 문자열을 의미

```elixir
# at : 문자열의 특정 위치를 리턴
iex(51)> String.at("dog", 0)
"d"

iex(52)> String.at("dog", -1)
"g"

# capitalize 형태로 변환
iex(53)> String.capitalize("dog") 
"Dog"

# 코드포인트 단위의 리스트로 리턴
iex(54)> String.codepoints("hello 리")
["h", "e", "l", "l", "o", " ", "리"]

# downcase, upcase
iex(56)> String.downcase("Dog")
"dog"

iex(37)> String.upcase "Dog"
"DOG"

# 문자열 * 횟수
iex(57)> String.duplicate("Hi~", 3)
"Hi~Hi~Hi~"

# 문자열이 해당 문자열(들)로 끝나는가? 또는 시작하는가?
iex(1)> String.ends_with?("hello", ["lla", "llb", "llc", "llo"])
true

iex(2)> String.ends_with?("hello", "llo")                       
true

iex(22)> String.starts_with? "hello", "he"
true

iex(23)> String.starts_with? "hello", ["he", "ke", "ie"]
true


# 문자열의 첫, 마지막 문자를 리턴
iex(3)> String.first "dog"
"d"

iex(4)> String.last "dog" 
"g"

# 문자열의 길이
iex(5)> String.length "hello 리"
7

# 패딩 (전, 후)
iex(6)> String.pad_leading "cat", 5, ">"
">>cat"

iex(9)> String.pad_trailing "cat", 5, ">"
"cat>>"

# 출력가능 여부
iex(10)> String.printable? "dog"
true

iex(11)> String.printable? "\x00 a null"
false

# 문자열 변환, global 옵션에 따라 전체 변경여부 판단
iex(15)> String.replace "the cat on the mat", "at", "AT"
"the cAT on the mAT"

iex(16)> String.replace "the cat on the mat", "at", "AT", global: false
"the cAT on the mat"

# 문자열 뒤집기
iex(18)> String.reverse "cat"
"tac"

# 문자열 자르기
iex(19)> String.slice "the cat on the mat", 4, 3
"cat"

iex(21)> String.slice "the cat on the mat", -3, 3
"mat"

# 문자열 분리하기
iex(24)> String.split " the cat on the mat "
["the", "cat", "on", "the", "mat"]

iex(25)> String.split "the cat on the mat", "t"
["", "he ca", " on ", "he ma", ""]

iex(26)> String.split "the cat on the mat", ~r{[ae]}
["th", " c", "t on th", " m", "t"]

iex(27)> String.split "the cat on the mat", ~r{[ae]}, parts: 2
["th", " cat on the mat"]

iex(30)> String.split "the cat on the mat", ~r{[ae]}, parts: 3
["th", " c", "t on the mat"]

# 문자열 앞뒤 공백(문자) 제거
iex(31)> String.trim "\t hello \r\n"
"hello"

iex(32)> String.trim "!!!hello!!!", "!"
"hello"

# 문자열 앞(또는 뒤) 공백(문자) 제거
iex(33)> String.trim_leading "\t hello \r\n"
"hello \r\n"

iex(34)> String.trim_leading "!!!hello!!!", "!"
"hello!!!"

iex(35)> String.trim_trailing "\t hello \r\n"  
"\t hello"

iex(36)> String.trim_trailing "!!!hello!!!", "!"
"!!!hello"

# 유효한 유니코드 코드포인트로 구성되어 있는지?
iex(38)> String.valid? "테스트"
true

iex(39)> String.valid? "hello"
true

iex(40)> String.valid? <<0x00, 0x81>>
false
```

## 8. 바이너리와 패턴 매칭

* 바이너리에서 매우 중요한 법칙은 `애매하다면 필드의 타입을 명시하자` 이다.

* 바이너리에서 사용가능한 타입은 `binary`, `bits`, `bitstring`, `bytes`, `float`, `integer`, `utf8`, `utf16`, `utf32`

*  또, 조건을 추가할 수도 있다.

    + size(n): 필드의 크기(비트 단위)

    + signed, unsigned: 정수 필드에 대해 부호가 있는것으로 해석할지 여부

    + 엔디언: big, little, native

    + 하이픈을 사용해 여러 속성을 구분할 수 있다.

        > <<length::unsigned-integer-size(12), flags::bitstring-size(4)>> = data

## 9. 바이너리로 문자열 처리하기

* 문자열의 머리가 UTF-8 타입이고, 나머지 부분은 바이너리임을 명심하자.

```elixir
# utf-iterate.ex
defmodule Utf8 do
  def each(str, func) when is_binary(str), do: _each(str, func)

  defp _each(<<head :: utf8, tail :: binary>>, func) do
    func.(head)
    _each(tail, func)
  end

  defp _each(<<>>, _func), do: []
end

Utf8.each "테스트", fn char -> IO.puts char end
```

```elixir
iex(47)> c "utf-iterate.ex"
warning: redefining module Utf8 (current version defined in memory)
  utf-iterate.ex:1

53580
49828
53944
[Utf8]
```