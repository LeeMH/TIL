# 6. 모듈과 기명함수

## 1. 모듈

* 함수들의 묶음

## 2. 함수선언 방법

* `do .. end` 사이에 로직을 넣는다.

* 한줄짜리 코드는 선언부 뒤에 컴마를 넣고 `do:`로 축약해서 표현할수 있다.

* 일반적으로 한줄은 `do:` 여러줄은 `do .. end`를 사용한다.

```elixir
# times.exs
defmodule Times do
  def double1(n), do: n * 2

  def double2(n) do
    IO.puts "입력 #{n}"
    n * 2
  end
end
```

```elixir
# 모듈 컴파일
iex(3)> c "times.exs"   
warning: redefining module Times (current version defined in memory)
  times.exs:1

[Times]
iex(4)> Times.double1(3)
6
iex(5)> Times.double2(3)
입력 3
6
```

> 모듈 컴파일 : iex에서 c "컴파일할 파일명"

## 3. 기명함수의 패턴매칭

* 익명함수에서 패턴매칭시 파라메터부를 여러번 선언하는데 반해,

* 기명함수에서는 함수를 중복해서 선언한다

```elixir
# factorial.exs
defmodule Factorial do
  def of(0), do: 1
  def of(n), do: n * of(n-1)
end
```

```elixir
iex(6)> c "factorial.exs"
[Factorial]

iex(7)> Factorial.of(5)
120
```

* 패턴매칭에서 `함수 선언 순서`는 아주 중요 하다.

* 아래와 같은 경우는 잘못된 예시이다.

```elixir
# factorial-bad.exs
defmodule Factorial do
  def of(n), do: n * of(n-1)
  def of(0), do: 1  
end
```

## 4. 가드 조건절(guard clause)

* 가드 조건절은 when 키워드를 사용해 함수의 정의부에 붙일수 있는 명제(predicate)이다.

* 파라메터를 통해 패턴매칭을 수행하고 when이 있는 경우 조건을 검하고 참인 경우만 수행된다.

```elixir
defmodule Guard do

  def what_is(x) when is_number(x) do
    IO.puts "#{x} is a number"
  end

  def what_is(x) when is_list(x) do
    IO.puts "#{inspect(x)} is a list"
  end

  def what_is(x) when is_atom(x) do
    IO.puts "#{x} is a atom"
  end

end
```

* 파라메터와 when 조건에 맞는 함수가 수행된다.

```elixir
iex(8)> c "guard.exs"
[Guard]

iex(9)> Guard.what_is(7)
7 is a number
:ok

iex(10)> Guard.what_is(:ok)
ok is a atom
:ok

iex(11)> Guard.what_is([1, 2, 4])
[1, 2, 4] is a list
:ok

iex(12)> Guard.what_is("hello")  
** (FunctionClauseError) no function clause matching in Guard.what_is/1    
    
    The following arguments were given to Guard.what_is/1:
    
        # 1
        "hello"
    
    guard.exs:2: Guard.what_is/1
```

* 앞의 factorial은 음수값을 넣거나 문자열을 넣으면 에러가 발생한다.

* 아래와 같이 가드를 추가하여 개선할수 있다.

```elixir
defmodule Factorial do
  def of(0), do: 1
  def of(n) when is_integer(n) and n > 0, do: n * of(n-1)
end
```

```elixir
iex(12)> c "factorial.exs"
warning: redefining module Factorial (current version defined in memory)
  factorial.exs:1

[Factorial]

iex(13)> Factorial.of(5)
120

iex(14)> Factorial.of(-3)
** (FunctionClauseError) no function clause matching in Factorial.of/1    
    
    The following arguments were given to Factorial.of/1:
    
        # 1
        -3
    
    factorial.exs:2: Factorial.of/1

iex(14)> Factorial.of("hello")
** (FunctionClauseError) no function clause matching in Factorial.of/1    
    
    The following arguments were given to Factorial.of/1:
    
        # 1
        "hello"
    
    factorial.exs:2: Factorial.of/1
```

## 5. 가드 조건절 주의사항

* 논리 연산자는 and, or, not을 사용해야 한다.

* &&, ||, !은 사용할 수 없다.

* 타입 확인 함수들

    + is_atom
    + is_binary
    + is_bitstring
    + is_boolean
    + is_exception
    + is_float
    + is_function
    + is_integer
    + is_list
    + is_map
    + is_map_key
    + is_nil
    + is_number
    + is_pid
    + is_port
    + is_reference
    + is_struct
    + is_tuple

## 6. 기본값

* 기명함수를 선언할때 각 파라메터에 `param \\ value` 문법을 사용해 기본값을 지정할 수 있다.

* 인자 개수 < 필수 파라메터 개수 : 매칭되지 않는다.

* 인자 개수 == 필수 파라메터 개수 : 순서대로 필수 파라메터에 매칭되고 나머지는 기본값을 가진다.

* 인자 개수 > 필수 파라메터 개수 : 넘치는 수만큼 순서대로 기본값을 덮어 쓴다.

```elixir
defmodule Example do
  def func(p1, p2 \\ 2, p3 \\ 3, p4) do
    IO.inspect [p1, p2, p3, p4]
  end
end
```

```elixir
iex(18)> c "default_param.exs" 
warning: redefining module Example (current version defined in memory)
  default_param.exs:1

[Example]
iex(19)> Example.func("a", "b")
["a", 2, 3, "b"]
["a", 2, 3, "b"]

# a, c, 3, b로 되지 않는다. 먼저 개수를 맞추고 순서대로 할당한다!!
iex(20)> Example.func("a", "b", "c")
["a", "b", 3, "c"]
["a", "b", 3, "c"]

iex(21)> Example.func("a", "b", "c", "d")
["a", "b", "c", "d"]
["a", "b", "c", "d"]
```

* 아래 모듈은 컴파일 되지 않는다.

* 왜냐하면, 첫번째 함수가 2, 3, 4개의 파라메터(arity)를 가지기 때문이다.

```elixir
defmodule Example do
  def func(p1, p2 \\ 2, p3 \\ 3, p4) do
    IO.inspect [p1, p2, p3, p4]
  end

  def func(p1, p2) do
    IO.inspect [p1, p2]
  end
end
```

```elixir
iex(23)> c "default_param.exs"
warning: redefining module Example (current version defined in memory)
  default_param.exs:1


== Compilation error in file default_param.exs ==
** (CompileError) default_param.exs:6: def func/2 conflicts with defaults from func/4
    default_param.exs:6: (module)
** (CompileError)  compile error
    (iex 1.12.2) lib/iex/helpers.ex:202: IEx.Helpers.c/2
```

## 7. 기본값 혼동 방지

* 아래의 경우 혼동이 발생할수 있는 케이스이다.

* 컴파일러는 의도적으로 경고를 발생시킨다.

```elixir
defmodule Example do
  def func(p1, p2 \\ 123) do
    IO.inspect [p1, p2]
  end

  def func(p1, 99) do
    IO.puts "you said 99"
  end
end
```

```elixir
iex(23)> c "default_param.exs"
warning: redefining module Example (current version defined in memory)
  default_param.exs:1

warning: variable "p1" is unused (if the variable is not meant to be used, prefix it with an underscore)
  default_param.exs:6: Example.func/2

warning: def func/2 has multiple clauses and also declares default values. In such cases, the default values should be defined in a header. Instead of:

    def foo(:first_clause, b \\ :default) do ... end
    def foo(:second_clause, b) do ... end

one should write:

    def foo(a, b \\ :default)
    def foo(:first_clause, b) do ... end
    def foo(:second_clause, b) do ... end

  default_param.exs:6

warning: this clause for func/2 cannot match because a previous clause at line 2 always matches
  default_param.exs:6

[Example]

iex(24)> Example.func("a", 99)           
["a", 99]
["a", 99]
```

* 이런 혼동을 없애기 위해서는 `기본값  파라메터가 있는 함수를 본문없이 정의`하고 나머지 함수들을 기본값 없이 정의하면 된다.

```elixir
defmodule Example do
  def func(p1, p2 \\ 123) 

  def func(p1, p2) when p2 !== 99 do
    IO.inspect [p1, p2]
  end

  def func(p1, p2) when p2 === 99 do
    IO.puts "you said 99"
  end
end
```

```elixir
iex(25)> c "default_param.exs"
warning: redefining module Example (current version defined in memory)
  default_param.exs:1

warning: variable "p1" is unused (if the variable is not meant to be used, prefix it with an underscore)
  default_param.exs:8: Example.func/2

[Example]

iex(26)> Example.func("a", 99)
you said 99
:ok
```

## 8. 프라이빗 함수

* 프라이빗 함수는 `defp`를 이용해 선언할수 있다.

## 9. 파이프 연산자 |>

* `파이프 연산자 |>` 를 이용해 함수와 출력을 다음 함수의 입력으로 연결할 수 있다.

```elixir
iex(28)> (1..10) |> Enum.map(&(&1 * &1)) |> Enum.filter(&(&1 < 40))
[1, 4, 9, 16, 25, 36]
```

* 파이프 연산은 `가독성` 뿐만 아니라, 명세와 닮은 꼴로 사용할 수 있다.

## 10. 모듈 지시자

### import

* 다른 모듈의 함수와 매크로를 현재 스코프로 가져온다.

```elixir
defmodule Example do
  def fun1 do
    List.flatten [1, [2, 3], 4]
  end

  def fun2 do
    import List, only: [flatten: 1]
    flatten [5, [6, 7], 8]
  end
end
```

### alias

* 모듈에 별칭을 생성한다.

* 주목적은 타이핑을 줄이는 것이다.

```elixir
defmodule Example do
  def compile_and_go(source) do
    alias My.Other.Module.Parser, as: Parser
    alias My.Other.Module.Runner, as: Runner

    source
    |> Parser.parse()
    |> Runner.execute()
  end
end
```

* as 부분을 생략하고 아래와 같이 줄여 쓸수 있다.

```elixir
alias My.Other.Module.Parser
alias My.Other.Module.Runner
```

* 더 줄여서 아래와 같은 표현도 가능하다.

```elixir
alias My.Other.Module.{Parser, Runner}
```

### require

* 다른 모듈에 정의된 매크로를 호출할때 사용. 22장에서 다시 설명

## 11. 모듈 속성(module attribute)

* 모듈은 메타데이터를 가질 수 있고, 이를 `모듈속성(model attribute)`라고 한다.

* `@속성이름 값` 형식으로 표현한다.

* 상수처럼 많이 사용하지만, 가급적 설정과 메타데이터 용도로만 사용하는것을 권장한다.

```elixir
defmodule Example do
  @author "hello"

  def get_author do
    @author
  end
end
```

## 12. 모듈의 이름

* 모듈 이름은 하나의 atom 이고, 대문자로 시작하는 모듈의 이름은 내부적으로 `"Elixir.모듈명"`의 atom으로 변경된다.

```elixir
iex(39)> is_atom IO
true

iex(40)> to_string IO
"Elixir.IO"

iex(42)> :"Elixir.IO" === IO
true

iex(43)> :"Elixir.IO".puts "hello"
hello
:ok
```

## 13. 모듈, 라이브러리 찾기

* 공식 웹사이트

* https://hex.pm

* 깃헙에서 elixir 검색

* 얼랭 라이브러리

    + 얼랭에서 변수는 대문자로 시작
    + 소문자로 시작하는 식별자는 atom