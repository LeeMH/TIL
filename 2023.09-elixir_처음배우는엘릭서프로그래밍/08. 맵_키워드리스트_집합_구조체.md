# 8. 맵, 키워드리스트, 집합, 구조체

## 1. 무엇을 써야 할까?

|질문|사용할 자료형|
|:---|:---|
|내용을 기준으로 패턴매칭 하고 싶은가?|맵|
|(예를 들어 :name 이라는 키가 있는 경우를 패턴매칭 하고 싶은가?)||
|같은 키로 여러 항목을 저장해야 하는가?|키워드 리스트|
|요소들의 순서가 보장되어야 하는가?|키워드 리스트|
|필드가 정해져 있는가?(즉, 데이터의 구조가 항상 동일한가?)|구조체|
|어떤것에도 해당하지 않는 경우|맵|

## 2. 키워드 리스트

* 키워드 리스트는 일반적으로 함수에 옵션을 전달할때 사용한다.

```elixir
defmodule Canvas do
  @defaults [ fg: "black", bg: "white", font: "Arial" ]

  def draw_text(text, options \\ []) do
    options = Keyword.merge(@defaults, options)
    IO.puts "Drawing text #{inspect(text)}"
    IO.puts "Foregroud: #{options[:fg]}"
    IO.puts "Backgroud: #{Keyword.get(options, :bg)}"
    IO.puts "Font: #{Keyword.get(options, :font)}"
    IO.puts "Pattern: #{Keyword.get(options, :patter, "solid")}"
    IO.puts "Style(get): #{Keyword.get(options, :style)}"
    IO.puts "Style(get_values): #{inspect Keyword.get_values(options, :style)}"
  end
end
```

```elixir
iex(36)> c "example.exs"                                                     
warning: redefining module Canvas (current version defined in memory)
  example.exs:1

[Canvas]
iex(37)> Canvas.draw_text("hello", fg: "red", style: "italic", style: "bold")
Drawing text "hello"
Foregroud: red
Backgroud: white
Font: Arial
Pattern: solid
Style(get): italic
Style(get_values): ["italic", "bold"]
:ok
```

## 3. 맵

* 맵은 랜덤 액세스가 가능한 키/밸류 자료구조로 크기에 상관없이 좋은 성능을 낸다.

```elixir
iex(39)> map = %{name: "Dave", likes: "programming", where: "Dallas"}
%{likes: "programming", name: "Dave", where: "Dallas"}

iex(40)> Map.keys map
[:likes, :name, :where]

iex(41)> Map.values map
["programming", "Dave", "Dallas"]

iex(42)> map[:name]
"Dave"

iex(43)> map.name
"Dave"

iex(44)> map1 = Map.drop map, [:where, :likes]
%{name: "Dave"}

iex(45)> map2 = Map.put map, :also_likes, "ruby"
%{also_likes: "ruby", likes: "programming", name: "Dave", where: "Dallas"}

iex(46)> Map.keys map2
[:also_likes, :likes, :name, :where]
```

## 4. 맵 패턴매칭하기

```elixir
iex(50)> person = %{name: "Dave", height: 1.88}
%{height: 1.88, name: "Dave"}

# map에 key가 존재하는가?
iex(51)> %{name: a_name} = person
%{height: 1.88, name: "Dave"}
iex(52)> %{first_name: a_name} = person
** (MatchError) no match of right hand side value: %{height: 1.88, name: "Dave"}

# map에 나열된 key가 모두 존재하는가?
iex(52)> %{name: _, height: _} = person
%{height: 1.88, name: "Dave"}
iex(53)> %{name: _, age: _} = person   
** (MatchError) no match of right hand side value: %{height: 1.88, name: "Dave"}

# map에 key/value가 모두 존재하는가?
iex(53)> %{name: "Dave"} = person
%{height: 1.88, name: "Dave"}
iex(54)> %{name: "Lee"} = person 
** (MatchError) no match of right hand side value: %{height: 1.88, name: "Dave"}
```

# 5. 맵 패턴매칭 필터링

* 패턴매칭과 필터링을 동시에 진행할수 있다

```elixir
people = [
  %{name: "John", height: 1.95},
  %{name: "Mary", height: 1.25},
  %{name: "Bob", height: 1.75},
  %{name: "Alice", height: 1.95}
]

IO.inspect(for person = %{height: height} <- people, height > 1.9, do: person)
```

```elixir
iex(55)> c "example2.exs"
[%{height: 1.95, name: "John"}, %{height: 1.95, name: "Alice"}]
```

* 함수 선언부에 when 구문을 조합하여 필터링 할수도 있다.

```elixir
defmodule HotelRoom do
  def book(%{name: name, height: height})
  when height > 1.9 do
    IO.puts "need extra large bed for #{name}"
  end

  def book(%{name: name, height: height})
  when height < 1.3 do
    IO.puts "need low shower controll for #{name}"
  end

  def book(person) do
    IO.puts "need standard room for #{person.name}"
  end
end
```

```elixir
iex(3)> people |> Enum.each(&(HotelRoom.book(&1)))
need extra large bed for John
need low shower controll for Mary
need standard room for Bob
need extra large bed for Alice
:ok
```

## 6. 맵 key 패턴 매칭 제약

* 패턴 매칭을 하더라도 key 자체는 바인딩 하지 않는다.

```elixir
iex(6)> %{2 => state} = %{1 => :ok, 2 => :error}
%{1 => :ok, 2 => :error}

iex(7)> state
:error

iex(8)> %{item => state} = %{1 => :ok, 2 => :error}
** (CompileError) iex:8: cannot use variable item as map key inside a pattern. Map keys in patterns can only be literals (such as atoms, strings, tuples, and the like) or an existing variable matched with the pin operator (such as ^some_var)
    (stdlib 3.17) lists.erl:1267: :lists.foldl/3
```

* 변수에 저장된 key로는 매칭이 가능하다.

```elixir
iex(8)> data = %{name: "Dave", state: "TX", likes: "programming"}
%{likes: "programming", name: "Dave", state: "TX"}

iex(9)> for key <- [:name, :likes] do
...(9)>   %{^key => value} = data
...(9)>   value
...(9)> end
["Dave", "programming"]
```

## 7. 맵 수정하기

* 리스트와 달리 맵의 수정은 전체를 순회하지 않아도 가능하다.

* 하지만 역시 원본의 데이터가 수정되는것이 아니고, 수정된 새로운 객체를 리턴하는것이다.

```elixir
iex(12)> m = %{a: 1, b: 2, c: 3}
%{a: 1, b: 2, c: 3} 

iex(13)> m1 = %{m | b: "two", c: "three"}  
%{a: 1, b: "two", c: "three"}

iex(14)> m2 = %{m1 | a: "one"}
%{a: "one", b: "two", c: "three"}
```

* 이 문법은 기존값을 변경하는것이고, 새로운 값을 넣기 위해서는 Map.put_new/3 함수를 이용해야 한다.

## 8. 구조체

* 타입이 있는 맵이 필요하다면?

* 필드가 정해져 있고, 그 필드들의 기본값이 있으며, 저장된 데이터 뿐만 아니라 맵의 타입 자체로도 패턴 매칭을 하고 싶다면?

* 이럴때 `구조체(struct)`를 사용한다.

* 구조체는 사실 맵의 제한된 형태를 모듈로 감싼것에 지나지 않는다.

* 타입에 상관없이 모든 값을 키로 사용할수 있는 맵과 달리 구조체의 키는 항상 아톰이어야 한다.

* 모듈 내에서 `defstruct` 매크로를 사용해 구조체의 필드를 정의 할수 있다.

```elixir
defmodule Subscriber do
  defstruct name: "", paid: false, over_18: true
end
```

```elixir
iex(2)> s1 = %Subscriber{}
%Subscriber{name: "", over_18: true, paid: false}
iex(3)> s2 = %Subscriber{name: "Dave"}
%Subscriber{name: "Dave", over_18: true, paid: false}
```

* 구조체를 모듈안에서 정의하는 이유는 구조체에 특화된 연산이 필요하기 때문이다.

```elixir
defmodule Attendee do
  defstruct name: "", paid: false, over_18: true

  def may_attend_after_party(attendee = %Attendee{}) do
    attendee.paid && attendee.over_18
  end

  def print_vip_badge(%Attendee{name: name}) when name != "" do
      IO.puts "very cheap badge for #{name}"
  end

  def print_vip_badge(%Attendee{}) do
    raise "missing name for badge"
  end
end
```

```elixir
iex(5)> a1 = %Attendee{name: "dave", over_18: true}
%Attendee{name: "dave", over_18: true, paid: false}

iex(6)> Attendee.may_attend_after_party(a1)
false

iex(7)> a2 = %Attendee{a1 | paid: true}
%Attendee{name: "dave", over_18: true, paid: true}

iex(9)> Attendee.may_attend_after_party(a2)
true
```

## 9. 중첩된 구조체 구조

* 자료형의 값으로 맵이 들어갈 수 있다.

```elixir
defmodule Customer do
  defstruct name: "", company: ""
end

defmodule BugReport do
  defstruct owner: %Customer{}, detail: "", severity: 1
end
```

* 접근은 아래와 같이 온점으로 접근 가능하다.

* 하위 맵에 대한 값의 변경 역시 맵의 값 변경과 비슷하다.

```elixir
iex(11)> report = %BugReport{owner: %Customer{name: "Dave", company: "hello"}, detail: "broken"} 
%BugReport{
  detail: "broken",
  owner: %Customer{company: "hello", name: "Dave"},
  severity: 1
}

iex(12)> report.owner.company
"hello"

# 수정
iex(13)> report = %BugReport{report | owner: 
...(13)>   %Customer{ report.owner | company: "new company"}}
%BugReport{
  detail: "broken",
  owner: %Customer{company: "new company", name: "Dave"},
  severity: 1
}
```

* 변경이 되긴 했으나, 번잡하다. 엘릭서에는 중첩된 딕셔너리에 쉽게 접근해 주도록 함수가 있다.

* 그중 하나인 `put_in`은 중첩된 자료구조내에 값을 저장해 준다.

```elixir
iex(14)> put_in(report.owner.company, "new-company2")
%BugReport{
  detail: "broken",
  owner: %Customer{company: "new-company2", name: "Dave"},
  severity: 1
}
```

* `update_in`은 자료구조내의 특정값에 함수를 적용한다.

```elixir
iex(15)> update_in(report.owner.name, &("Mr. " <> &1))
%BugReport{
  detail: "broken",
  owner: %Customer{company: "new company", name: "Mr. Dave"},
  severity: 1
}
```

## 10. 집합

* 집합은 MapSet 모듈로 구현되어 있다.

```elixir
iex(16)> set1 = 1..5 |> Enum.into(MapSet.new)
#MapSet<[1, 2, 3, 4, 5]>

iex(17)> set2 = 3..8 |> Enum.into(MapSet.new)
#MapSet<[3, 4, 5, 6, 7, 8]>

iex(18)> MapSet.member? set1, 3
true

iex(19)> MapSet.union set1, set2
#MapSet<[1, 2, 3, 4, 5, 6, 7, 8]>

iex(20)> MapSet.difference set1, set2
#MapSet<[1, 2]>
```